archivesBaseName += "-1.19-compat"

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version_1_19_2}"

    minecraft "com.mojang:minecraft:${project.minecraft_version_1_19_2}"
    //noinspection GradlePackageVersionRange
    mappings "net.fabricmc:yarn:${project.yarn_mappings_1_19_2}:v2"

    modLocalRuntime("net.fabricmc.fabric-api:fabric-api:${project.fabric_version_1_19_2}")

    modLocalRuntime("com.simibubi.create:create-fabric-${project.minecraft_version_1_19_2}:${project.create_version_1_19_2}")
    switch (project.recipe_viewer.toLowerCase(Locale.ROOT)) {
        case "rei": {
            modLocalRuntime("dev.architectury:architectury-fabric:${project.architectury_version_1_19_2}")
            modLocalRuntime("me.shedaniel:RoughlyEnoughItems-fabric:$project.rei_version_1_19_2")
            break
        }
        case "emi": modLocalRuntime("dev.emi:emi:${project.emi_version}+${project.minecraft_version_1_19_2}"); break
        case "disabled": break
        default: modLocalRuntime("mezz.jei:${project.jei_version_1_19_2}"); break
    }
}

loom {
    runs {
        clientVersioned {
            client()
            name = "Minecraft Client 1.19.2"
            runDir = "../run/client"
            ideConfigGenerated = true
        }
        serverVersioned {
            server()
            name = "Minecraft Server 1.19.2"
            runDir = "../run/server"
            ideConfigGenerated = true
        }
    }
}